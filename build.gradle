buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.3.1'
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
        classpath "org.ajoberstar:gradle-git:1.3.2"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.17"
        classpath 'com.github.erizo.gradle:jcstress-gradle-plugin:0.8.0'
    }
}

apply plugin: 'idea'
apply from: "${rootDir}/libraries.gradle"

ext {
    releaseVersion = '0.12.1'
}

allprojects {

    version = '0.13.0-SNAPSHOT'
    group = 'io.github.resilience4j'
    description = 'Resilience4j is a lightweight, easy-to-use fault tolerance library designed for Java8 and functional programming'

    repositories {
        jcenter()
        mavenCentral()
    }
}

ext {
    coreProjects = subprojects.findAll {
        p -> !p.name.contains("documentation")
    }
}

configure(project.coreProjects) {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply from: "${rootDir}/publishing.gradle"
    dependencies {
        compile ( libraries.vavr)
        compile ( libraries.slf4j)

        testCompile ( libraries.junit)
        testCompile ( libraries.assertj)
        testCompile ( libraries.logback)
        testCompile ( libraries.mockito)
        testCompile ( libraries.powermock)
        testCompile ( libraries.powermock_api_mockito)
        testCompile ( libraries.powermock_module_junit4)
        testCompile ( libraries.awaitility)
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
        options.deprecation = true
        options.encoding = 'UTF-8'
        options.compilerArgs += ["-Xlint:unchecked", "-parameters"]

    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}
